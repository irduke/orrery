con

  _clkfreq = 300_000_000      'System clock set at 300 MHz

  EOCCW = 8
  EOCW = 9
  EOEncoder = 10 'includes pin 11 as well
  EOProxHome = 12
  Home = 13

  p_gain = 10

var

  long positionEO, Stack1[100], Stack2[100]

pub Main()

  GoHome()

  debug(`term term1 pos 40 40 size 10 4 textsize 42 color blue)                           'Set up a debug terminal window for Earth Orbit Position

  'cogspin(2, EarthOrbitMotor(),@Stack2)

  repeat
    debug(`term1 '`(positionEO)' 10)           'Display current rotary position on the debug terminal (10 = new line)

    if ina.[Home]==1
      cogstop(1)
      cogstop(2)
      GoHome()
      waitms(3000)
      cogspin(1, Encoder(),@Stack1)
      cogspin(2, EarthOrbitMotor(),@Stack2)

    waitms(200)

PUB Encoder()

  'Start quadrature encoder smartpin reader watching Pin 10 (Apin=%0000) and Pin 11 (Bpin=%0001) for Moon Orbit
  pinstart(EOEncoder, p_quadrature + %0000<<28 + %1001<<24, 0, 0)

  repeat
    positionEO:=rdpin(EOEncoder) 'Earth Orbit                    'Read quadrature encoder's overall count that's automatically being stored in the smartpin
                                                        'For 25D Pololu metal gearmotor, counts per revolution (CPR) of quadrature encoder = gear ratio * 48

pub GoHome() | x

  'Set upper and lower words of Xval so smartpins will output 30kHz PWM signals
  x.word[0] := 10     'Set base period's # of clock pulses 10*3.33ns=33ns
  x.word[1] := 1000   'Set PMW frame period to be made up of 1000 base periods

  pinstart(EOCW, p_oe+p_pwm_sawtooth, x, 0)        'Start PWM signals
  pinstart(EOCCW, p_oe+p_pwm_sawtooth, x, 0)

  repeat until ina.[EOProxHome]==0
    wypin(EOCW,0)
    wypin(EOCCW,1000)

  'Stop Motors
  wypin(EOCW,0)
  wypin(EOCCW,0)

  waitms(1000)

pub EarthOrbitMotor() | x

  'Set upper and lower words of Xval so smartpins will output 30kHz PWM signals
  x.word[0] := 10     'Set base period's # of clock pulses 10*3.33ns=33ns
  x.word[1] := 1000   'Set PMW frame period to be made up of 1000 base periods

  pinstart(EOCW, p_oe+p_pwm_sawtooth, x, 0)     'Start PWM signals
  pinstart(EOCCW, p_oe+p_pwm_sawtooth, x, 0)

  repeat
    wypin(EOCW,0)
    wypin(EOCCW,0)
    waitus(50)
    GoEO(5742)
    waitms(1000)
    GoEO(0)
    waitms(1000)
    GoEO(2781)
    waitms(1000)

pub GoEO(targetEO)
  repeat 5
    if rdpin(EOEncoder)==targetEO
      return
    elseif rdpin(EOEncoder)<targetEO
      wypin(EOCW,0)
      wypin(EOCCW,1000)
      repeat until rdpin(EOEncoder)>=targetEO
        wypin(EOCCW,(50#> abs(rdpin(EOEncoder)-targetEO)*p_gain <#1000))
    elseif rdpin(EOEncoder)>targetEO
      wypin(EOCCW,0)
      wypin(EOCW,1000)
      repeat until rdpin(EOEncoder)<=targetEO
        wypin(EOCW,(50#> abs(rdpin(EOEncoder)-targetEO)*p_gain <#1000))
  'Stop Motor
  wypin(EOCW,0)
  wypin(EOCCW,0)
  waitus(50)